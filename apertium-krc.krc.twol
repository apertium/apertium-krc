Alphabet

 А а  Б б  В в  Г г  Д д  Е е
 Ё ё  Ж ж  З з  И и  Й й  К к  Л л
 М м  Н н  О о  П п  Р р  С с  Т т
 У у  Ў ў  Ф ф  Х х  Ц ц  Ч ч  Ш ш  Щ щ
 Ъ ъ  Ы ы  Ь ь  Э э  Ю ю  Я я  

 %{I%}:ы %{I%}:и %{I%}:у %{I%}:ю %{I%}:0
 %{y%}:ы %{y%}:и %{y%}:у %{y%}:ю %{y%}:0
 %{G%}:г %{G%}:х %{G%}:0
 %{D%}:д %{D%}:т
 %{N%}:н ! %{N%}:0
 %{R%}:р %{R%}:л %{R%}:н
 %{н%}:н %{н%}:0
 %{A%}:а %{A%}:е 
 %{E%}:а %{E%}:е %{E%}:й
 %{ъ%}:ъ %{ъ%}:0
 %{s%}:с %{s%}:0
 %{р%}:р %{р%}:0
 %{n%}:н %{n%}:0

 %{·%}:0
 %{☭%}:0

 %-

 %>:0  ;

Sets

Vow =     А Э И О У Я Е Ы Ё Ю 
          а э и о у я е ы ё ю ; 

FrontVow = Э Е И
           э е и ;

BackVow = О Ы У 
          о ы у ;

Cns =     Б В Г Д Ж З Й К Л М Н О П Р С Т Ф Х Ц Ч Ш Щ 
          б в г д ж з й к л м н о п р с т ф х ц ч ш щ 
          %{s%} %{n%} %{р%} %{G%} %{D%} %{L%} %{N%} ; 

CnsRight = Б В Г Д Ж З Й К Л М Н О П Р С Т Ф Х Ц Ч Ш Щ Ъ 
           б в г д ж з й к л м н о п р с т ф х ц ч ш щ ъ ; 

AbstrCns = %{s%} %{n%} %{р%} %{G%} %{D%} %{L%} %{N%} ; 

RealCns = Б В Г Д Ж З Й К Л М Н О П Р С Т Ф Х Ц Ч Ш Щ
          б в г д ж з й к л м н о п р с т ф х ц ч ш щ ; 

Rules 

"Remove soft sign with suffix"
ь:0 <=> _ %>: [ Cns: | Vow: ] ;


"Deletion of {s} after a consonant"
%{s%}:0 <=> :CnsRight/[ :0 - AbstrCns: ] _ ;
                 AbstrCns:/[ %>: | %-: ] _ ;
                           :BackVow  :у  %>: _ ;
          except 
            [ [ ? :Vow ] - [  :BackVow :у ] ] %>: _ ;

"Surfacing of {s} after a vowel"
%{s%}:с <=> [ [ ? :Vow ] - [ :BackVow :у ] ] %>: _ ;

! о:о н:н о:о у:у >:0 {s}:0 {I}:у {n}:н >:0 {D}:д {A}:а 

"Epenthesis in some stems"
%{y%}:0 <=> _ [ :Cns :Vow ]/[ :0 | %>: ] ;

"Plural -р- only appears before possessives"
%{р%}:р <=> _ %>: [ %{s%}: | %{I%}: м | %{I%}: н г | %{I%}: б %{I%}: з | %{I%}: г %{ъ%}: %{I%}: з ] ; 

"{G} of dative deletes after 1-2px"
%{G%}:0 <=> [ %{I%}: м | %{I%}: н г | %{n%}: ] %>: _ %{ъ%}: %{A%}: ; 

"{ъ} of dative deletes after 1-2px"
%{ъ%}:0 <=> [ %{I%}: м | %{I%}: н г | %{n%}: ] %>: %{G%}: _ %{A%}: ; 
                                                          _ :FrontVow ; 
                                             :FrontVow :к _ ;
                     [ :с | :б | :т | :ш | :т ь: ] %>: :х _ :а  ; 
              [ :с | :б | :т | :ш | :т ь: ] %>: %{н%}: :х _ :а  ; 
                                     :н %>: %{н%}: %{G%}: _ %{A%}: ; 

! а:а т:т >:0 {I}:ы м:м >:0 {G}:0 {ъ}:0 {A}:а

! с:с а:а й:й л:л а:а н:н >:0 {н}:н {G}:г {ъ}:0 {A}:а н:н >:0 д:д {I}:ы 

! б:б а:а ш:ш л:л а:а н:н >:0 {н}:н {G}:г {ъ}:0 {A}:а н:н >:0 д:д {I}:ы 

"{G} is х after -т- and -ш-"
%{G%}:х <=> [ :с | :б | :т | :ш | :т ь: ] %>: _ %{ъ%}: :а  ;
            [ :с | :б | :т | :ш | :т ь: ] %>: %{н%}: _ %{ъ%}: :а  ;

! к:к ъ:ъ а:а т:т ы:ы ш:ш >:0 {н}:0 {G}:х {ъ}:0 {A}:а н:н >:0 д:д {I}:ы 

! а:а х:х ы:ы р:р >:0 {s}:0 {I}:ы {n}:н >:0 {G}:0 {ъ}:0 {A}:а 

! д:д ж:ж а:а н:н >:0 {I}:0 г:г {ъ}:ъ {I}:ы з:з 

! к:к е:е ч:ч >:0 {I}:и р:р {A}:е к:к {ъ}:0

! и:и н:н с:с т:т и:и т:т у:у т:т >:0 {G}:х {ъ}:0 {A}:а	

! к:к е:е с:с е:е к:г >:0 {s}:0 {I}:и {n}:0

! к:к ю:ю н:н б:б а:а т:т ы:ы ш:ш >:0 {G}:х {ъ}:0 {A}:а

! д:д е:е >:0 {G}:г {ъ}:0 {A}:е н:н

"Voicing between vowels"
к:г <=> :Vow _ (:ъ) %>: (:0) :Vow ;  

! к:к е:е с:с е:е к:г >:0 {s}:0 {I}:и {n}:0

! а:а р:р а:а л:л ы:ы к:г ъ:ъ >:0 {s}:0 {I}:ы {n}:0

